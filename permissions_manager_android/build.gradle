apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

/**
 * Fixes this error:
 *
 * FAILURE: Build failed with an exception.
 *
 * * What went wrong:
 * Execution failed for task ':permissions_manager_android:javadoc'.
 * > Javadoc generation failed. Generated Javadoc options file (useful for troubleshooting):
 * '/Users/ralphpina/Documents/Android-Permissions-Manager/permissions_manager_android/build/tmp/javadoc/javadoc.options'
 */
tasks.withType(Javadoc).all { enabled = false }

ext {

    bintrayRepo = "maven"
    bintrayName = "permissions-manager-android"

    publishedGroupId = 'net.ralphpina.permissionsmanager.android'
    libraryName = 'permissions-manager-android'
    artifact = 'permissions-manager-android'

    libraryDescription = 'Easily manage Android Marshmallow and up runtime permissions. This library is backwards compatible. In pre-Marshmallow devices permissions are returned as given.'

    siteUrl = 'https://github.com/ralphpina/Android-Permissions-Manager'
    gitUrl = 'https://github.com/ralphpina/Android-Permissions-Manager.git'

    libraryVersion = '3.0.0'

    developerId = 'ralphpina'
    developerName = 'Ralph Pina'
    developerEmail = 'ralph.pina@gmail.com'

    licenseName = 'The MIT License'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "2.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    // gets rid of this message:
    // java.lang.RuntimeException: Method putExtra in android.content.Intent not mocked. See http://g.co/androidstudio/not-mocked for details.
    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {
    implementation project(':permissions_manager')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha02'

    // TESTING
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.3.21'
    testImplementation "com.google.truth:truth:0.43"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
}

//Add these lines to publish library to bintray. This is the readymade scripts made by github user nuuneoi to make uploading to bintray easy.
//Place it at the end of the file
if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/wajahatkarim3/JCenter-Gradle-Scripts/master/install.gradle'
    apply from: 'https://raw.githubusercontent.com/wajahatkarim3/JCenter-Gradle-Scripts/master/bintray.gradle'
}
